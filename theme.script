## Author : Osi Bluber
## Github : @OsiPog

// Screen size
screen.w = Window.GetWidth(0);
screen.h = Window.GetHeight(0);

// State
state.status = "normal";

// Init images
for (i = 0; i < 89; i++)
  loop_image[i] = Image("colorful_loop/progress-" + i + ".png");
loop_sprite = Sprite();
loop_sprite.SetX(Window.GetX() + (screen.w / 2 - loop_image[0].GetWidth() / 2)); # Place images in the center
loop_sprite.SetY(Window.GetY() + (screen.h / 2 - loop_image[0].GetHeight() / 2));

for (i = 0; i < 48; i++)
  circle_alt_image[i] = Image("circle_alt/progress-" + i + ".png");
circle_alt_sprite = Sprite();
circle_alt_sprite.SetX(Window.GetX() + (screen.w / 2 - circle_alt_image[0].GetWidth() / 2)); # Place images in the center
circle_alt_sprite.SetY(Window.GetY() + (screen.h / 2 - circle_alt_image[0].GetHeight() / 2));

lock_left_sprite = Sprite();
lock_image = Image("lock_left.png");
lock_left_sprite.SetImage(lock_image);
lock_left_sprite.SetX(Window.GetX() + (screen.w / 2 - lock_image.GetWidth() / 2)); # Place images in the center
lock_left_sprite.SetY(Window.GetY() + (screen.h / 2 - lock_image.GetHeight() / 2));
lock_left_sprite.SetZ(10);

lock_right_sprite = Sprite();
lock_image = Image("lock_right.png");
lock_right_sprite.SetImage(lock_image);
lock_right_sprite.SetX(Window.GetX() + (screen.w / 2 - lock_image.GetWidth() / 2)); # Place images in the center
lock_right_sprite.SetY(Window.GetY() + (screen.h / 2 - lock_image.GetHeight() / 2));
lock_left_sprite.SetZ(11);

lock_sprite = Sprite();
lock_image = Image("lock.png");
lock_sprite.SetImage(lock_image);
lock_sprite.SetX(Window.GetX() + (screen.w / 2 - lock_image.GetWidth() / 2)); # Place images in the center
lock_sprite.SetY(Window.GetY() + (screen.h / 2 - lock_image.GetHeight() / 2));
lock_sprite.SetZ(12);
lock_sprite.SetOpacity(0);

t = 0;
loop_has_played = 0;
circle_alt_opacity = 0;
lock_opacity = 0;

fun ApplyVector(sprite, vx, vy) {
    x = sprite.GetX();
    y = sprite.GetY();
    sprite.SetX(x + Math.Int(vx));
    sprite.SetY(y + Math.Int(vy));
}

fun RefreshCallback ()
  {
    index_loop = Math.Int(t / 1.75) % 89;
    index_circle = Math.Int(t / 2.4) % 48;

    if (Plymouth.GetMode() == "boot") {
        circle_alt_sprite.SetOpacity(circle_alt_opacity);
        lock_left_sprite.SetOpacity(lock_opacity);
        lock_right_sprite.SetOpacity(lock_opacity);

        // Always play loop once at the start
        if (loop_has_played == 0) {
            loop_sprite.SetImage(loop_image[index_loop]);
            t++;
            if (index_loop >= 88) {
                loop_has_played = 1;
                loop_sprite.SetOpacity(0);
                t = 0;
            }
        }

        if ((loop_has_played == 1) && (state.status == "password")) {
            if (lock_opacity < 1) {
                lock_opacity += 0.05;
                lock_sprite.SetOpacity(lock_opacity);
            }
        }

        if ((loop_has_played == 1) && (state.status == "normal")) {
            if (circle_alt_opacity < 1) {
                circle_alt_opacity += 0.01;
            }
            // lock break animation
            if (lock_opacity > 0) {
                lock_sprite.SetOpacity(0);
                lock_opacity -= 0.05;
            }

            circle_alt_sprite.SetImage(circle_alt_image[index_circle]);
            t++;
        }

    }

    if (Plymouth.GetMode() == "shutdown") {
      circle_alt_sprite.SetImage(circle_alt_image[index_circle]);
      t++;
    }
  }

Plymouth.SetRefreshFunction(RefreshCallback);

//------------------------------------- Password prompt -------------------------------

fun DisplayPasswordCallback (prompt, bullets) {
    state.status = "password";
}

Plymouth.SetDisplayPasswordFunction(DisplayPasswordCallback);

//--------------------------- Normal display (unset all text) ----------------------
fun DisplayNormalCallback() {
    state.status = "normal";
}
Plymouth.SetDisplayNormalFunction(DisplayNormalCallback);

//----------------------------------------- Message --------------------------------
fun MessageCallback(text) {
    message.image = Image.Text(text, 1, 1, 1);
    message.sprite = Sprite(message.image);
    message.sprite.SetPosition(screen.half.w - message.image.GetWidth() / 2, message.image.GetHeight());
}
Plymouth.SetMessageFunction(MessageCallback);
